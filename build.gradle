plugins {
    id 'java'
    id 'idea'
    id 'jacoco'
    id 'org.springframework.boot' version '2.7.4'
    id 'io.spring.dependency-management' version '1.0.14.RELEASE'
    id "org.sonarqube" version "3.4.0.2513"
}

group 'com.felipe'
version '1.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

jacoco {
    toolVersion = "0.8.8"
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
    afterEvaluate {
        classDirectories.from = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    'com/felipe/todolist/infraestructure/config/**',
                    'com/felipe/todolist/infraestructure/model/**',
                    'com/felipe/todolist/domain/model/**',
                    'com/felipe/todolist/TodoListApp*'
            ])
        })
    }
}

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
    maven {
        url 'https://maven.aliyun.com/nexus/content/groups/public/'
    }
}

ext {
    openapiUIVersion = '1.6.11'
    lombokVersion = '1.18.24'

    junitVersion = '5.9.0'
    restAssureVersion = '5.2.0'
    mockitoVersion = '4.8.0'
}

sonarqube {
    properties {
        property "sonar.projectKey", "felipecristancho10_Proyecto_final"
        property "sonar.organization", "felipecristancho10"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.coverage.exclusions", "**/model/**", "**/config/**"
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation "org.springdoc:springdoc-openapi-ui:${openapiUIVersion}"

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testImplementation "io.rest-assured:rest-assured:${restAssureVersion}"
    testImplementation "io.rest-assured:xml-path:${restAssureVersion}"
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.mockito:mockito-inline:${mockitoVersion}"
    testImplementation "io.rest-assured:spring-mock-mvc:${restAssureVersion}"

    // https://mvnrepository.com/artifact/mysql/mysql-connector-java
    implementation 'mysql:mysql-connector-java:8.0.31'
    // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-data-jdbc
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc:2.7.5'

    implementation 'org.mapstruct:mapstruct:1.5.2.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.2.Final'
    testAnnotationProcessor 'org.mapstruct:mapstruct-processor:1.5.2.Final'

}

test {
    useJUnitPlatform()
}